plugins {
    id "com.github.dcendents.android-maven" version "2.0"
    id "com.android.library"
}

ext {
    customVersionCode = 1
    customVersionName = '1.0.0'
    customGroupId = 'com.ikecin.sdk' /**记得同步更改AndroidManifest.xml中的package */
    customArtifactId = 'demo'
}

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 22
        versionCode customVersionCode
        versionName customVersionName
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'consumer-proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
}

/**
 * 注意 group、version、artifactId 只对本地maven发布有效，对gitpack无效
 */
apply plugin: 'com.github.dcendents.android-maven'
group = customGroupId
version = customVersionCode

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    options {
        encoding 'utf-8'
        charSet 'utf-8'
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/** 使用命令 gradlew uploadArchives 即可发布到本地maven */
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: rootDir.toPath().resolve('maven').toUri())
            //repository(url: "file://D:\\YangHeng\\Documents\\AndroidStudioProjects\\Maven")
            pom.groupId = customGroupId
            pom.version = customVersionName
            pom.artifactId = customArtifactId
        }
    }
}
